LHSName  "Nusantara-trans-novowels"
RHSName  "UNICODE"

define xspace U+0020 ; spasi " "
Define a U+0061  ;a
Define b U+0062  ;b
Define c U+0063  ;c
Define d U+0064  ;d
Define e U+0065  ;e
Define f U+0066  ;f
Define g U+0067  ;g
Define h U+0068  ;h
Define i U+0069  ;i
Define j U+006A  ;j
Define k U+006B  ;k
Define l U+006C  ;l
Define m U+006D  ;m
Define n U+006E  ;n
Define o U+006F  ;o
Define p U+0070  ;p
Define q U+0071  ;q
Define r U+0072  ;r
Define s U+0073  ;s
Define t U+0074  ;t
Define u U+0075  ;u
Define v U+0076  ;v
Define w U+0077  ;w
Define x U+0078  ;x
Define y U+0079  ;y
Define z U+007A  ;z
Define A U+0041  ;A
Define B U+0042  ;B
Define D U+0044  ;D
Define E U+0045  ;E
Define G U+0047  ;G
Define H U+0048  ;H
Define I U+0049  ;I
Define N U+004E  ;N
Define O U+004F  ;O
Define S U+0053  ;S
Define T U+0054  ;T
Define U U+0055  ;U
Define W U+0057  ;W
Define X U+0058  ;W
Define Y U+0059  ;Y
Define Z U+005A  ;Z
Define C U+005E  ;^  << CARET
Define UL U+005F ;_  << UNDERLINE
Define XUNDERLINE U+005F ;_  << UNDERLINE
Define XTILDE U+007E ;_  << TILDE
Define P U+002E  ;.  << DOT
Define V U+002C  ;,  << COMMA (_V_irgule)
Define CO U+003A ;:  << COLON
Define SC U+003B ;; << SEMICOLON
Define Ain U+0060 ;`
Define GRAVE U+0060 ; `
Define XHAMZA U+0027 ; '  << HAMZA << APOSTHROPE
Define QUOTE U+0022  ;"  << QUOTE << FORCED HAMZA
Define XHYPHEN U+002D  ;-  << HYPHEN
Define BAR U+007C ;|
Define AN  U+0061 U+004E ; aN
Define LILLAH U+006C U+0069 U+006C U+006C U+0061 U+0068 ; ll_ah
Define LLAH U+006C U+006C U+005F U+0061 U+0068 ; ll_ah
;Define NUL U+00A6;
Define Ha  U+0623
Define Hi  U+0625
Define Hy  U+0626
Define Hw  U+0624
Define Hh  U+06C0 ;; = U+0647 U+0654
Define HA  U+0622
Define HB  U+0640 U+0654
Define HBAR U+0621 ; '|
Define ALIF U+0627 ; |
Define BA U+0628 ; |
Define TA U+062A ; |
Define WAW U+0648 ; |
Define YA U+064A ; |
Define LAM U+0644
Define DZAL U+0630
Define FA U+0641
Define KAF U+0643
Define ALIFLAYYINAH U+0649
Define DEBUG U+0058 ; X
Define TASYDID U+0651
Define HAMZAA  U+0623 ; hamza above alif
Define HAMZAI  U+0625 ; hamza under alif
Define HAMZAY  U+0626 ; hamza above alif layyinah
Define HAMZAW  U+0624 ; hamza above waw
Define HAMZAWASHOL  U+0671 ; alif with washl atop
Define HAMZASINGLE U+0621 ; single hamza

Define ALIFMAD  U+0622; alif with mad 3x
Define HAMZADEBUG  U+06C0 ; hamza above ha
Define BACKSLASHSPACE U+00A0 ;; "\ "

Define LLAHar U+0644 U+0644 U+0647 ; 'LLAH'
Define HYPHENARABIC U+06D4
Define CANDIDATEHAMZA U+06FF
Define FATHA U+0618
Define DLAMMA U+0619
Define KASRA U+061A

; 1st pass
pass(Unicode)

U+E000 > ; eliminate special code for capitalization in transliterations

; 2nd pass
pass(Unicode)

Class [Digits] = (U+0030..U+0039)
; special ligatures
;; 1. Standard ArabTeX input
;; FDF2 is just used as temporary holder, and is converted back to ligature lam lam hah later (to be taken care of by the font)
;; The BAR is used in the DMG transliteration to suppress the initial A
(a|A) BAR? l XHYPHEN LLAH > U+0627 U+FDF2 ;; 'al-ll_ah' or 'Al-ll_ah'
LLAH > U+FDF2  ;; 'll_ah' -> 'l l h'

;; UPPERCASE mnemonics for various ligatures or special glyphs
S W S >  U+0627 U+0644 U+0635 U+0644 U+0627 U+0629 xspace U+0648 xspace U+0627 U+0644 U+0633 U+0644 U+0627 U+0645 ; 'al-shalaa:tu wa al-salaamu'
U+004C U+004C U+0048 > U+FDF2  ;; 'LLH' -> 'l l h'
U+0046 U+0041 U+004C U+0049 U+004C U+004C U+0041 U+0048 > U+0641 U+064E U+0644 U+0650 U+0644 U+0647 ;; 'FALILLAH'
U+0041 U+004C U+004C U+0041 U+0048 > U+0627 U+0644 U+0644 U+0647 ; 'ALLAH'
U+004C U+004C U+0041 U+0048 > U+0644 U+0644 U+0647 ; 'LLAH'
;U+0041 U+004C U+004C U+0041 U+0044 U+005A U+0049 > ALIF LAM DZAL ALIFLAYYINAH ; 'ALLADZI'

U+0053 U+004C U+004D > U+FDFA ; 'SLM' : .sallY al-ll_ah `alayhi wa-sallam
U+0028 U+0028 > U+FD3F ;; '((' : ornate right par
U+0029 U+0029 > U+FD3E ;; '))' : ornate left par
U+0052 U+0049 U+0059 U+0041 U+004C > U+FDFC ;; 'RIYAL' : Saudi currency sign
;; most fonts won't have the following ones:
U+0053 U+0041 U+004C U+004C U+0041 U+0053 U+0054 U+004F U+0050 > U+FDF0 ;; 'SALLASTOP' (.sallY with "ye-barree", Quranic stop sign)
U+0051 U+0041 U+004C U+0041 > U+FDF1 ;; 'QALA' : qalY with ye-barree, Quranic stop sign
U+0041 U+004B U+0042 U+0041 U+0052 > U+FDF3 ;; 'AKBAR'
U+004D U+0055 U+0048 U+0041 U+004D U+004D U+0041 U+0044 A > U+FDF4 ALIF ;; 'MUHAMMAD'
U+004D U+0055 U+0048 U+0041 U+004D U+004D U+0041 U+0044 > U+FDF4 ;; 'MUHAMMAD'
U+0053 U+0041 U+004C U+0041 U+004D > U+FDF5 ;; 'SALAM'
U+0052 U+0041 U+0053 U+004F ? U+0055 U+004C > U+FDF6 ;; 'RASUL' (also unicode 'RASOUL')
U+0041 U+004C U+0041 U+0059 U+0048 (U+0049|U+0045) > U+FDF7 ;; 'ALAYHI' (also unicode 'ALAYHE')
U+0057 U+0041 U+0053 U+0041 U+004C U+004C U+0041 U+004D > U+FDF8 ;; 'WASALLAM'
U+0053 U+0041 U+004C U+004C U+0041 > U+FDF9 ;; 'SALLA'
U+004A U+0041 U+004C U+004C U+0041 > U+FDFB ;; 'JALLA' : "jalla jalAluhu"
U+0042 U+0041 U+0053 U+004D U+0041 U+004C U+0041 > U+FDFD ;; 'BASMALA' : very few fonts have this ligature afaik
U+005B U+005B ([Digits]+)=dig U+005D U+005D > U+06DD @dig ; [[digits]]

U+005E U+0053 U+004C U+0059 > U+06D6 ;; ^SLY
U+005E U+0051 U+004C U+0059 > U+06D7 ;; ^QLY
U+005E U+004D U+0049 U+004D > U+06D8 ;; ^MIM-
U+005E U+004C U+0041 > U+06D9 ;; ^LA
U+005E U+004A U+0049 U+004D > U+06DA ;; ^JIM
U+005E U+0044 U+004F U+0054 U+0053 > U+06DB ;; ^DOTS
U+005E U+0053 U+0049 U+004E > U+06DC ;; ^SIN
U+0048 U+0049 U+005A U+0042 > U+06DE ;; HIZB
U+0043 U+0049 U+0052 U+0043 U+005A U+0045 U+0052 U+004F > U+06DF ;; CIRCZERO
U+0052 U+0045 U+0043 U+0054 U+005A U+0045 U+0052 U+004F > U+06E0 ;; RECTZERO
U+005E U+004A U+0041 U+005A U+004D > U+06E1 ;; ^JAZM
U+005E U+004D U+0049 U+004D > U+06E2 ;; ^MIM
U+005F U+0053 U+0049 U+004E > U+06E3 ;; _SIN
U+005E U+004D U+0041 U+0044 U+0044 U+0041 > U+06E4 ;; ^MADDA
U+0057 U+0041 U+0057 > U+06E5 ;; WAW
U+0059 U+0045 U+0048 > U+06E6 ;; YEH
U+005E U+0059 U+0045 U+0048 > U+06E7 ;; ^YEH
U+005E U+004E U+0055 U+004E > U+06E8 ;; ^NUN
U+0053 U+0041 U+004A U+0044 U+0041 > U+06E9 ;; SAJDA
U+005F U+0053 U+0054 U+004F U+0050 > U+06EA ;; _STOP
U+005E U+0053 U+0054 U+004F U+0050 > U+06EB ;; ^STOP
U+005E U+0052 U+0053 U+0054 U+004F U+0050 > U+06EC ;; ^RSTOP
U+005F U+004D U+0049 U+004D > U+06ED  ;; _MIM

U+0044 U+004F U+0054 U+0053 > U+061E ;; DOTS  (also possible by typing "::")

;; 4rd pass
pass(Unicode)

; hamzah awal kata
Class [RomanVowel]         = (U+0061  U+0065  U+0069  U+006F  U+0075 ) ;(a e i o u)
Class [BRACKETS]    = (U+003C U+003E U+00AB U+00BB U+0028 U+0029 U+005B U+005D U+2018 U+2019 U+201C U+201D); < > () « » [ ] ‘ ’ “ ”
Class [PUNCTUATION] = (V SC P CO)

Define Newline U+000A

Define BEG  (#|Z|[BRACKETS]|[PUNCTUATION]|Newline|XHYPHEN|xspace)
Define END  (#|[BRACKETS]|[PUNCTUATION] U+0020|[PUNCTUATION]|Newline|xspace #)
Define ENDZ (#|[BRACKETS]|[PUNCT] U+0020|[PUNCT] #|Z)

XUNDERLINE [RomanVowel]=v1 > ALIF @v1 ; niatnya washol
XTILDE [RomanVowel]=v1 > HAMZAWASHOL @v1 ; niatnya washol
[RomanVowel] l XHYPHEN / BEG _ > ALIF l XHYPHEN
; Force Hamza and Lock, will be released at the end of all pass
QUOTE y > HAMZAY X
QUOTE i > HAMZAI X
QUOTE w > HAMZAW X
QUOTE A > HAMZAA X
QUOTE a > HAMZASINGLE X
a / BEG _ > HAMZAA a
i / BEG _ > HAMZAI i
u / BEG _ > HAMZAA u

pass(Unicode) ; imla' hamzah
i XHAMZA > i HAMZAY
u XHAMZA i > u HAMZAY i
u XHAMZA > u HAMZAW
a XHAMZA i > a HAMZAY i
a XHAMZA u > a HAMZAW u
a XHAMZA > a HAMZAA
XHAMZA a > HAMZAA a
XHAMZA i > HAMZAY i
XHAMZA u > HAMZAW u

pass(Unicode)
a a a > ALIFMAD
a a > ALIF
i i > YA
u u > WAW
a u > a w
a i > a y

pass(Unicode)
Class [RomanVowel]         = (U+0061  U+0065  U+0069  U+006F  U+0075 ) ;(a e i o u)
Class [RomanConsonant] = (U+0062..U+0064 U+0066..U+0068 U+006A..U+006E U+0070..U+0074 U+0076..U+007A U+005E U+002E U+005F U+0022 U+0027 U+0060) ;a..z ^ . _ " ' ` without a i u e o
Class [RomanLetter] = (U+0061..U+007A U+0041 U+0045 U+0047 U+0049 U+004F U+0055 U+0042 U+004E U+0054 U+0059 U+005E U+002E U+005F U+0027 U+0060) ;a..z A E I O U B N T Y ^ _ " "" `

pass(Unicode)

Class [Digits] = (U+0030..U+0039)
Class [ArDigits] = (U+0660..U+0669)
;++;Class [ArEasternDigits] = (U+06F0..U+06F9)
Class [VWL]         = (U+0061  U+0065  U+0069  U+006F  U+0075 ) ;(a e i o u)
;++;Class [LTR]      = (U+0061..U+007A U+0041 U+0045 U+0049 U+004F U+0055 U+0042 U+004E U+0054 U+0059 U+005E U+002E U+005F U+0022 U+0027 U+0060) ;(a..z A E I O U B N T Y ^ . _ " "" `)
;++;Class [Iy]      = (U+0049 U+0079) ;(I y)
Class [Uw]          = (U+0055 U+0077) ;(U w)

;normal code
a N > ALIF ; FATHATEN

b b                                                       >  U+0628 U+0651  ;;SHADDA
b                                                         >  U+0628 ; ba
t t                                                       >  U+062A U+0651  ;;SHADDA
t                                                         >  U+062A ;ta
t s t s >  U+062B U+0651  ;;SHADDA
t s     >  U+062B ; tsa
C (b|t) C (b|t)                                         >  U+062B U+0651  ;;SHADDA
C (b|t)                                                  >  U+062B ; tsa
j j                                                       >  U+062C U+0651  ;;SHADDA
j                                                         >  U+062C ; jim
P j P j >  U+062D U+0651  ;;SHADDA
P j >  U+062D ; _ha
UL h UL h >  U+062D U+0651  ;;SHADDA
UL h >  U+062D ; _ha
(UL j) (UL j)                                 >  U+062D U+0651  ;;SHADDA
(UL j)                                              >  U+062D ; ha
k h k h                                       >  U+062E U+0651  ;;SHADDA
k h                                                     >  U+062E ; kho
P (h) P (h)                                      >  U+062E U+0651  ;;SHADDA
P (h)                                                      >  U+062E ; kho
d d                                                       >  U+062F U+0651  ;;SHADDA
d                                                         >  U+062F ; dal
d z d z   >  U+0630 U+0651  ;;SHADDA
d z       >  U+0630 ; dzal
P d P d                                         >  U+0630 U+0651  ;;SHADDA
P d                                                  >  U+0630 ; dzal
r r                                                       >  U+0631 U+0651  ;;SHADDA
r                                                         >  U+0631 ; ra
z z                                                       >  U+0632 U+0651  ;;SHADDA
z                                                         >  U+0632 ; zai
s s                                                       >  U+0633 U+0651  ;;SHADDA
s                                                         >  U+0633 ; sin
C s C s                                                   >  U+0634 U+0651  ;;SHADDA
C s                                                       >  U+0634 ; syin
s y s y                                 >  U+0634 U+0651  ;;SHADDA
s y                                              >  U+0634 ; syin
S S                                 >  U+0635 U+0651  ;;SHADDA
S                                              >  U+0635 ; shod
s h s h                                 >  U+0635 U+0651  ;;SHADDA
s h                                              >  U+0635 ; shod
(P S) (P S)                                 >  U+0636 U+0651  ;;SHADDA
(P S)                                              >  U+0636 ; shod
d l d l >  U+0636 U+0651  ;;SHADDA
d l >  U+0636 ; dlod
D D                                  >  U+0636 U+0651  ;;SHADDA
D                                              >  U+0636 ; dlod
t h t h >  U+0637 U+0651  ;;SHADDA
t h >  U+0637 ; tho
T T                                 >  U+0637 U+0651  ;;SHADDA
T                                              >  U+0637 ; tho
z h z h                                 >  U+0638 U+0651  ;;SHADDA
z h                                              >  U+0638 ; zho
Z Z                                 >  U+0638 U+0651  ;;SHADDA
Z                                              >  U+0638 ; zho
(P T) (P T)                                 >  U+0638 U+0651  ;;SHADDA
(P T)                                              >  U+0638 ; dzo
Ain Ain                                                   >  U+0639 U+0651  ;;SHADDA
Ain                                                       >  U+0639 ; 'ain
g h g h >  U+063A U+0651 ;;SHADDA
g h >  U+063A ; ghain
(P A|g) (P A|g)                                                 >  U+063A U+0651 ;;SHADDA
(P A|g)                                                      >  U+063A ; ghain
f f                                                       >  U+0641 U+0651  ;;SHADDA
f                                                         >  U+0641 ; fa
q q                                                       >  U+0642 U+0651  ;;SHADDA
q                                                         >  U+0642 ;qa
k k                                                       >  U+0643 U+0651  ;;SHADDA
k                                                         >  U+0643 ;kaf
l l                                                       >  U+0644 U+0651  ;;SHADDA
l                                                         >  U+0644 ;lam
m m                                                       >  U+0645 U+0651  ;;SHADDA
m                                                         >  U+0645 ;mim
n n                                                       >  U+0646 U+0651  ;;SHADDA
n                                                         >  U+0646 ;nun
h h                                                       >  U+0647 U+0651  ;;SHADDA
h                                                         >  U+0647 ;ha
H H                                                       >  U+0647 U+0651  ;;SHADDA
H                                                         >  U+0647 ;ha
CO t                                                      >  U+0629 ; ta marbuthoh
CO H CO H                                                 >  U+0629 U+0651 ;,SHADDA
CO H AN                                                      >  U+0629 ;ta marbuthoh
CO H                                                      >  U+0629 ; ta marbuthoh
W                                                         >  U+0648 U+0627 ; waw + alif
C U                                                       >  U+064F U+0648 U+0653; historical spelling : madda on waw
[Uw] [Uw]                                                 >  U+0648 U+0651  ;;SHADDA
[Uw]                                                      >  U+0648 ; waw
y y                                                       >  U+064A U+0651  ;;SHADDA
y                                                         >  U+064A ; ya
Y Y                                                       >  U+0649 U+0651  ;;SHADDA
Y                                                         >  U+0649 ; alif layyinah

; additional
BAR > ALIF

;pegon
c c                                                       >  U+0686  U+0651 ;;SHADDA
c                                                         >  U+0686

p p                                                       >  U+06A4  U+0651 ;;SHADDA
p                                                         >  U+06A4
C g C g                                                   >  U+06A0 U+0651 ;;SHADDA
C g                                                       >  U+06A0 ; ng
G G                                                   >  U+06AE U+0651 ;;SHADDA
G                                                       >  U+06AE ; ng

XHYPHEN XHYPHEN / [Digits] _ [Digits]                >  U+2013  ; two hyphens in numerical context -> endash
; hyphen or brackets in numerical context: surrounded by LRO & PDF marks
(XHYPHEN|U+005D|U+005B|U+0028|U+0029)=xx / [Digits] _ [Digits]  >  U+202D @xx U+202C
[Digits]                                             >  [ArDigits]
XHYPHEN XHYPHEN XHYPHEN / # _ #                      >  U+2014
XHYPHEN XHYPHEN / # _ #                              >  U+2013
XHYPHEN XHYPHEN                                      >  U+0640


pass(Unicode)
Class [Signs] = (U+002C U+002E) ; Comma, FullStop.
Class [VWL]         = (U+0061  U+0065  U+0069  U+006F  U+0075 ) ;(a e i o u)

pass(Unicode)
Class [VWL]         = (U+0061  U+0065  U+0069  U+006F  U+0075 ) ;(a e i o u)
[VWL] >
Define ALIFCANDIDATE U+06F6
Define YACANDIDATE U+06FD
Define WAWCANDIDATE U+06C6
a > ALIFCANDIDATE ; U+06FD
i > YACANDIDATE
u > WAWCANDIDATE

pass(Unicode) ; imla' hamzah aturan dobel huruf
ALIF HAMZAA > ALIF HAMZASINGLE
WAW HAMZAW > WAW HAMZASINGLE
HAMZAA ALIF > ALIFMAD
HAMZAA ALIFMAD > ALIFMAD

pass(Unicode)
Class [BRACKETS]    = (U+003C U+003E U+00AB U+00BB U+0028 U+0029 U+005B U+005D U+2018 U+2019 U+201C U+201D); < > () « » [ ] ‘ ’ “ ”
Class [PUNCTUATION] = (V SC P CO)
(FA|TA|BA|KAF)=f1 LAM XHYPHEN /BEG _ > @f1 ALIF LAM XHYPHEN
LAM XHYPHEN LAM > LAM TASYDID
LAM LLAHar > LLAHar
(FA|TA|BA|KAF)=f1 LLAHar > @f1 ALIF LLAHar

pass(Unicode) ; imla' hamzah untuk akhir kata
Class [HAMZAALL] = (HAMZAA HAMZAI HAMZAW HAMZAY)
Class [BRACKETS]    = (U+003C U+003E U+00AB U+00BB U+0028 U+0029 U+005B U+005D U+2018 U+2019 U+201C U+201D); < > () « » [ ] ‘ ’ “ ”
Class [BRACKETSar] = (U+FD3F U+FD3E)
Class [PUNCTUATION] = (V SC P CO)
Class [RomanVowel]         = (U+0061  U+0065  U+0069  U+006F  U+0075 ) ;(a e i o u)
Class [ArabicLetter] = (U+0600..U+06FF)
Class [EndOfAnything] = (Newline xspace U+0003 U+000A U+0004 U+000D)
Define END3 (# ([BRACKETS]|[PUNCTUATION] xspace|[PUNCTUATION]|Newline|xspace|[BRACKETSar]))
[HAMZAALL] (xspace|Newline|[PUNCTUATION]|[BRACKETS]|[BRACKETSar]|[EndOfAnything])=p1 > HAMZASINGLE @p1

pass(Unicode)
Class [RomanLetter] = (U+0061..U+007A U+0041 U+0045 U+0047 U+0049 U+004F U+0055 U+0042 U+004E U+0054 U+0059 U+005E U+002E U+005F U+0027 U+0060) ;a..z A E I O U B N T Y ^ _ " "" `
XHYPHEN >
X > ; release Force Hamzah Lock
[RomanLetter] > ; temporary cleanup
U+0651 > ; TASYDID
U+002E >  U+06DD ;  U+06DE  ; Full Stop .
U+002C > U+060C ; Comma ,
